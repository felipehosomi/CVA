IF EXISTS (SELECT * FROM sys.objects WHERE type = 'P' AND name = 'SP_CVA_OP_ABERTA')
	DROP PROCEDURE SP_CVA_OP_ABERTA
GO
CREATE PROCEDURE SP_CVA_OP_ABERTA
(
	@ItemCode NVARCHAR(100)
)
AS
BEGIN
	SELECT
		OBPL.BPLName,
		OBPL.BPLFrName,
		OBPL.AliasName BPLAliasName,
		OADP.LogoImage LogoImage,
		OITM.ItemCode,
		OITM.ItemName,
		OITM.InvntryUom			[UN],
		OITM.BWeight1			[PesoLiquido],
		OITM.U_h_peso_bruto		[PesoBruto],
		OITM.U_h_desc_localizacao [Localizacao],
		ONCM.NcmCode,
		UFD1.Descr		[Grupo],
		OITB.ItmsGrpNam	[SubGrupo],
		AUPT.ErfUser	[Usuario],
		AUPT.BELNR_ID	[OP],
		AUPT.BELDAT		[Dt. Cadastro],
		AUPT.LFGDAT		[Dt. Entrega],
		AUPT.ANFZEIT	[Dt. Início],
		AUPT.KND_ID		[Cód. Cliente],
		AUPT.KNDNAME	[Cliente],
		POS.MENGE_VERBRAUCH	[Qtde],
		0.00			[Est Fís],
		0.00			[Est Res],
		0.00			[Est Enc],
		0.00			[Est Dis],
		OITW.MinStock	[Est Mín],
		APL.POS_ID		[N Proc.],
		APL.[G.M.],
		APL.Instrução,
		APL.[% Concluído],
		APL.[Dt. Inicial],
		APL.[Dt. Final],
		APL.QtdeProduzida
	FROM OITM WITH(NOLOCK)
		INNER JOIN OBPL WITH(NOLOCK)
			ON OBPL.BPLId = 1	
		INNER JOIN OITW WITH(NOLOCK)
			ON OITW.ItemCode = OITM.ItemCode
			AND OITW.WhsCode = OBPL.DflWhs
		INNER JOIN OITB WITH(NOLOCK)
			ON OITB.ItmsGrpCod = OITM.ItmsGrpCod
		INNER JOIN BEAS_FTHAUPT AUPT WITH(NOLOCK)
			ON AUPT.ItemCode =	OITM.ItemCode
		INNER JOIN BEAS_FTPOS POS WITH(NOLOCK)
			ON POS.BELNR_ID = AUPT.BELNR_ID
			AND POS.STUFE = 0
		INNER JOIN
		(
			SELECT APL.BELNR_ID, APL.BELPOS_ID, APL.POS_ID,
				CAST(APLATZ.BEZ AS NVARCHAR(MAX))	[G.M.],
				CAST(APL.BEZ AS NVARCHAR(MAX))		[Instrução],
				SUM(CASE WHEN APL.ABGKZ = 'J' THEN 1.00 ELSE 0.00 END) / COUNT(APL.ABGKZ) * 100 [% Concluído],
				SUM(ZEIT.MENGE_GUT)	[QtdeProduzida],
				MIN(ZEIT.ANFZEIT)	[Dt. Inicial],
				MAX(ZEIT.ENDZEIT)	[Dt. Final]
				FROM BEAS_FTAPL APL WITH(NOLOCK)
					INNER JOIN BEAS_APLATZ APLATZ WITH(NOLOCK)
						ON APLATZ.APLATZ_ID = APL.APLATZ_ID
					LEFT JOIN BEAS_ARBZEIT ZEIT WITH(NOLOCK)
						ON ZEIT.BELNR_ID = APL.BELNR_ID
						AND ZEIT.BELPOS_ID = APL.BELPOS_ID
				GROUP BY APL.BELNR_ID, APL.BELPOS_ID, APL.POS_ID,
				CAST(APLATZ.BEZ AS NVARCHAR(MAX)),
				CAST(APL.BEZ AS NVARCHAR(MAX))
		 ) APL
			ON APL.BELNR_ID = AUPT.BELNR_ID
			AND APL.BELPOS_ID = POS.BELPOS_ID
		LEFT JOIN ONCM WITH(NOLOCK)
			ON ONCM.AbsEntry = OITM.NcmCode
		LEFT JOIN  CUFD WITH(NOLOCK)
			ON CUFD.TableId = 'OITM'
			AND CUFD.AliasId = 'h_cod_grupo'
		INNER JOIN UFD1 WITH(NOLOCK)
			ON UFD1.TableId = CUFD.TableId
			AND UFD1.FieldId = CUFD.FieldId
			AND UFD1.FldValue = OITM.U_h_cod_grupo
		LEFT JOIN OADP WITH(NOLOCK)
			ON 1 = 1
	WHERE OITM.ItemCode = @ItemCode
END
