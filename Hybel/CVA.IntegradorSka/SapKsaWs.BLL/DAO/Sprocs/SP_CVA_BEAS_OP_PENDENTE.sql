IF EXISTS (SELECT * FROM sys.objects WHERE type = 'P' AND name = 'SP_CVA_BEAS_OP_PENDENTE')
	DROP PROCEDURE SP_CVA_BEAS_OP_PENDENTE
GO
CREATE PROCEDURE SP_CVA_BEAS_OP_PENDENTE
AS
BEGIN
	SELECT DISTINCT
		T0.BELNR_ID AS [OP] -- Número da Ordem de Produção
		, T1.nummer AS [OPER] -- Número da Operação Associada a Produção
		, T2.ItemCode AS [CODPECA] -- Código da peça associada a Produção
		, T3.UDF1 AS [MAQ] -- Código da Máquina Planejada
		, T2.ANFZEIT AS [PLANDTINI] -- Data Planejada para o Início da Produção
		, T2.ENDZEIT AS [PLANDTFIM] -- Data Planejada para o Fim da Produção
		, CAST(ISNULL(T2.MENGE, 0) AS INT) AS [PLANQTY] -- Quantidade Planejada de Peças
		, CAST(ISNULL(T4.UDF1, 0) AS INT) AS [CYCQTY] -- Quantidade de peças por ciclos de produção
		, T1.TEAPLATZ AS [PLANTMUNIT] -- Tempo planejado de um ciclo produtivo
	    , T1.TRAPLATZ AS [PLANTMSETUP]-- Tempo planejado de Setup -- ISNULL(T4.UDF2, 0) 
		, CASE lower(T5.AENDERUNGSTYP)
			WHEN 'änderung' THEN 2
			WHEN 'beas_qsartikelhaupt' THEN 2
			WHEN 'change' THEN 2
			WHEN 'crear' THEN 1
			WHEN 'criar' THEN 1
			WHEN 'del' THEN 3
			WHEN 'info' THEN 2
			WHEN 'löschen' THEN 3
			WHEN 'new' THEN 1
			ELSE 1
		END AS [ACAO] -- 1 - Inclusão; 2 - Alteração; 3 - Exclusão (ações que são feitas no BEAS)
		, 0 AS [STATUS] -- 0 - Não Processado; 2 - Alteração; 3 - Exclusão (manda sempre 0, a SKA usa o campo para atualizar)
		, T2.BELPOS_ID
		, T1.POS_ID	
		, ISNULL(T5.ErfTStamp,T1.ErfTStamp) TSTAMP
		, CASE WHEN ISNULL(T6.OP, '') <> ''
			THEN 1
			ELSE 0
		END Existe
	INTO #OPs
	FROM BEAS_FTHAUPT T0
		LEFT JOIN BEAS_FTAPL T1 ON T0.BELNR_ID = T1.BELNR_ID 
		LEFT JOIN BEAS_FTPOS T2 ON T0.BELNR_ID = T2.BELNR_ID AND T1.BELPOS_ID = T2.BELPOS_ID
		LEFT JOIN BEAS_APLATZ T3 ON T1.APLATZ_ID = T3.APLATZ_ID
		LEFT JOIN BEAS_APL T4 ON T3.APLATZ_ID = T4.APLATZ_ID AND T1.POS_ID = T4.POS_ID AND T1.AG_ID = T4.AG_ID AND T2.ItemCode = T4.ItemCode
		LEFT JOIN BEAS_AENDERUNG T5 ON CAST(T0.BELNR_ID AS NVARCHAR) = T5.KENNUNG1 AND CAST(T1.BELPOS_ID AS NVARCHAR) = T5.KENNUNG2 AND T5.TYP2 = 'WO'
		LEFT JOIN MES.dbo.SSPImport T6 ON T6.OP = T1.BELNR_ID AND T6.BELPOS_ID = T1.BELPOS_ID AND T6.POS_ID = T1.POS_ID
	WHERE T2.ItemCode IS NOT NULL
		AND (
			(
				ISNULL(T0.ABGKZ, 'N') = 'N'
				AND ISNULL(T0.UNVISIBLE, 0) != 1
			) OR T0.FLAG_ERROR = 1
		)
		--AND ISNULL(T5.ErfTStamp, '2000-01-01') >= ISNULL(T6.TSTAMP, '2000-01-01') 
	--ORDER BY T0.BELNR_ID, T2.BELPOS_ID, T1.POS_ID, T5.ErfTStamp, T1.ErfTStamp

	INSERT INTO MES.dbo.SSPImport
	(OP, BELPOS_ID, POS_ID, OPER, CODPECA, MAQ, PLANDTINI, PLANDTFIM, PLANQTY, CYCQTY, PLANTMUNIT, PLANTMSETUP, ACAO, STATUS, TSTAMP)
	SELECT OP, BELPOS_ID, POS_ID, OPER, CODPECA, MAQ, PLANDTINI, PLANDTFIM, PLANQTY, CYCQTY, PLANTMUNIT, PLANTMSETUP, ACAO, STATUS, ISNULL(TSTAMP, GETDATE()) FROM #OPs WHERE Existe = 0

	UPDATE T1
	SET OPER = T2.OPER, 
		CODPECA = T2.CODPECA, 
		MAQ = T2.MAQ, 
		PLANDTINI = T2.PLANDTINI, 
		PLANDTFIM = T2.PLANDTFIM, 
		TSTAMP = T2.TSTAMP, 
		PLANQTY = T2.PLANQTY, 
		CYCQTY = T2.CYCQTY, 
		PLANTMUNIT = T2.PLANTMUNIT, 
		PLANTMSETUP = T2.PLANTMSETUP, 
		ACAO = T2.ACAO
	FROM MES.dbo.SSPImport T1
		INNER JOIN #OPs T2 ON T2.OP = T1.OP AND T2.BELPOS_ID = T1.BELPOS_ID AND T1.POS_ID = T2.POS_ID

	DROP TABLE #OPs
END