IF EXISTS (SELECT * FROM sys.objects WHERE type = 'P' AND name = 'SP_CVA_SKA_BEAS_ENTRADA_ACABADO')
	DROP PROCEDURE SP_CVA_SKA_BEAS_ENTRADA_ACABADO
GO
CREATE PROCEDURE SP_CVA_SKA_BEAS_ENTRADA_ACABADO
AS
BEGIN
	SELECT DISTINCT
		PROD.Id			IdMes,
		PROD.OP,
		PROD.BELPOS_ID	BelPosId,
		PROD.POS_ID		PosId,
		PROD.CODPECA	ItemCode,
		REF.REJ			Refugo,
		IGN1.DocEntry	DocEntryEntrada,
		CAST(ERRO.Information AS NVARCHAR(MAX)) Erro
	FROM MES.[dbo].SSPExportProd PROD WITH(NOLOCK)
		INNER JOIN MES.[dbo].SSPExportProd_Log TLOG WITH(NOLOCK)
			ON TLOG.ID_MES = PROD.Id
			AND TLOG.[TYPE] = 3		-- Acabado
			AND TLOG.[Status] = 1	-- Arquivo gerado
		LEFT JOIN (SELECT SUM(REJ) REJ, OP, BELPOS_ID FROM MES.[dbo].SSPExportProd GROUP BY OP, BELPOS_ID) REF
			ON REF.OP = PROD.OP
			AND REF.BELPOS_ID = PROD.BELPOS_ID
		LEFT JOIN IGN1 WITH(NOLOCK)
			ON IGN1.u_belnrid = CAST(PROD.OP AS INT)
			AND IGN1.u_belposid = CAST(PROD.BELPOS_ID AS INT)
		OUTER APPLY
		(
			SELECT TOP 1 * FROM BEAS_PROTOKOLL ERRO WITH (NOLOCK)
			WHERE ERRO.BASE_DOCENTRY = PROD.OP
			AND ERRO.BASE_LINENUM = PROD.BELPOS_ID
			AND ERRO.BASE_DOCTYPE = 'WO'
			AND PROD.[STATUS] = 1
			AND ERRO.DATUM >= REPDATETIME
			ORDER BY NR DESC
		) ERRO
	WHERE ISNULL(IGN1.DocEntry, 0) <> 0
	OR ISNULL(CAST(ERRO.Information AS NVARCHAR(MAX)), '') <> ''
END

