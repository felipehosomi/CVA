; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "SAP AddOn Hub"
#define MyAppVersion "1.0.0.49"
#define MyAppPublisher "CVA"
#define MyAppURL "http://www.cvaconsultoria.com.br"
#define MyAppExeName "CVA.AddOn.Hub.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{09B3058D-E073-4962-B858-EF008EED895B}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={code:GetDefaultAddOnDir}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=Setup
Compression=lzma
SolidCompression=yes
ShowLanguageDialog=no
AllowNoIcons=yes
WizardImageFile="logo1.bmp"
WizardImageStretch=yes
WizardSmallImageFile="logo2.bmp"

[Languages]
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"

[LangOptions]
LanguageName=Brazilian Portuguese
LanguageID=$0416
LanguageCodePage=0
DialogFontName=Arial
DialogFontSize=8
WelcomeFontName=Verdana
WelcomeFontSize=12
TitleFontName=Arial
TitleFontSize=29
CopyrightFontName=Arial
CopyrightFontSize=8
RightToLeft=no

[Files]
Source: "C:\Source\Atlantic\EnvioNfLote\MEF\AddOnInstallAPI.dll"; Flags: dontcopy
Source: "C:\Source\Atlantic\EnvioNfLote\MEF\ISSkin.dll"; DestDir: {app}; Flags: dontcopy
Source: "C:\Source\Atlantic\EnvioNfLote\MEF\Vista.cjstyles"; DestDir: {tmp}; Flags: dontcopy
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\CVA.AddOn.Hub.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\ConverterLib.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\CVA.AddOn.Common.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\CVA.AddOn.Hub.Logic.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\Interop.SAPbobsCOM.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\Interop.SAPbouiCOM.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\Microsoft.Office.Interop.Word.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\Microsoft.Vbe.Interop.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\office.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\stdole.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\CVA.AddOn.Common.dll.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\CVA.AddOn.Hub.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\Microsoft.Office.Interop.Word.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\CVA.AddOn.Hub\bin\Debug\office.xml"; DestDir: "{app}"; Flags: ignoreversion
;Source: "C:\Source\Produtos\CVA.PefinOne\Setup\Ajuda.chm"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\Setup\AddOnInfo.xml"; Flags: dontcopy
Source: "C:\Source\DeSangosse\CVA.AddOn.Hub\Setup\listfile.txt"; Flags: dontcopy
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"

[UninstallDelete]
Type: filesandordirs; Name: "{app}\*.*"

[PostCompile]
;Name: "'C:\Program Files (x86)\SAP\SAP Business One SDK\Tools\AddOnRegDataGen\AddOnRegDataGen.exe' 'C:\Source\Produtos\CVA.PefinOne\Setup\AddOnInfo.xml' 0.0.0.5 'C:\Source\Produtos\CVA.PefinOne\Setup\Output\Setup.exe' 'C:\Source\Produtos\CVA.PefinOne\Setup\Output\Setup.exe' 'C:\Source\Produtos\CVA.PefinOne\CVA.PefinOne\bin\Debug\PefinOne.exe'"; Flags: runminimized cmdprompt redirectoutput
;Name: "'C:\Program Files\7-Zip\7z.exe' a -tzip 'C:\Source\Produtos\CVA.PefinOne\Setup\Output\CVA.PefinOne_v0.0.0.5.zip' @listfile.txt"; Flags: runminimized cmdprompt redirectoutput
Name: "C:\Source\DeSangosse\CVA.AddOn.Hub\Setup\AddOnRegDataGen.bat"; Flags: runminimized cmdprompt redirectoutput

[Code]
const
	ADDON_ID = 'SAP AddOn Hub';
	PARTNER_ID = 'CVA'; 
	
procedure LoadSkin(lpszPath: String; lpszIniFileName: String); external 'LoadSkin@files:isskin.dll stdcall';
procedure UnloadSkin(); external 'UnloadSkin@files:isskin.dll stdcall';

function ShowWindow(hWnd: Integer; uType: Integer): Integer; external 'ShowWindow@user32.dll stdcall';

//Funções externas (AddOnInstallAPI.dll)
function SetAddOnFolder(path: string): integer; external 'SetAddOnFolder@files:AddOnInstallAPI.dll stdcall';
function EndInstallEx(path: string; succeed: boolean): integer; external 'EndInstallEx@files:AddOnInstallAPI.dll stdcall';
function EndUninstall(path: string; succeed: boolean): integer; external 'EndUninstall@files:AddOnInstallAPI.dll stdcall';

function GetDLLPath(IsUninstalling: boolean): string;
var ValueName: string;
begin
    if IsUninstalling then begin
		   RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\' + PARTNER_ID + '\'	+ ADDON_ID, 'DLLInstallDir', ValueName);
			 Result := ValueName;
		end
		else
			 //Result := Copy(ParamStr(6), Pos('|', ParamStr(6))+1, Length(ParamStr(6))-Pos('|', ParamStr(6)));
			 //MsgBox('DirInstall:' + Copy(ParamStr(2), 0, Length(ParamStr(2))-Pos('|', ParamStr(2))), mbInformation, MB_OK);
			 Result:=Copy(ParamStr(2), 0, Length(ParamStr(2))-Pos('|', ParamStr(2)));  		
end;	

function GetDefaultAddOnDir(param: String): string;
begin
  //MsgBox(ParamStr(0), mbInformation, MB_OK);
  //MsgBox(ParamStr(1), mbInformation, MB_OK);
  //MsgBox(ParamStr(2), mbInformation, MB_OK);

  //MsgBox('DefaultDir:' + Copy(ParamStr(2), 1, Pos('|', ParamStr(2))-1), mbInformation, MB_OK);
  Result := Copy(ParamStr(2), 1, Pos('|', ParamStr(2))-1);
end;

// Grava no Registro a chave que identifica onde o add-on foi instalado
procedure RegisterAddOnInfo();
begin
  RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\' + PARTNER_ID + '\' + ADDON_ID, 'InstallDir', 'NMS');
  RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\' + PARTNER_ID + '\' + ADDON_ID, 'DLLInstallDir', GetDLLPath(false));
end;

procedure UnregisterAddOnInfo();
begin
  RegDeleteValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\' + PARTNER_ID + '\' + ADDON_ID, 'InstallDir');
  RegDeleteValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\' + PARTNER_ID + '\' + ADDON_ID, 'DLLInstallDir');
  RegDeleteKeyIfEmpty(HKEY_LOCAL_MACHINE, 'SOFTWARE\' + PARTNER_ID + '\' + ADDON_ID);
end;


function GetAddOnInstalledPath(): string;
var ValueName: string;
begin
  RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\' + PARTNER_ID + '\' + ADDON_ID, 'InstallDir', ValueName);
  Result := ValueName;
end;

//*******************************************************************************************************
//  InitializeSetup (função interna do Inno)
//  Executada sempre que a instalação/desinstalação inicia
//*******************************************************************************************************
function InitializeSetup(): boolean;
var ResultCode: integer;
begin
  //MsgBox(ParamStr(2), mbInformation, MB_OK);
  // Está desinstalando...
  if ParamStr(2) = '/u' then begin
		ExtractTemporaryFile('Vista.cjstyles');
		LoadSkin(ExpandConstant('{tmp}\Vista.cjstyles'), '');
    SetCurrentDir(GetDLLPath(true));
    Exec(GetAddOnInstalledPath + '\unins000.exe', '', '', SW_SHOW, ewWaitUntilTerminated, ResultCode)
    Result := false;
  end

  // Está na instalação...
  else begin

    // A instalação tentou ser executada fora do SBOne?
    //if Pos('|', ParamStr(6)) = 0 then begin                      //000
    //  MsgBox('A instalação deve ser executada a partir do SAP Business One.', mbInformation, MB_OK);
    //  Result := false;
    //end    //000
    // Ok, instalando de dentro do SBOne...
    //else begin //000

      // Muda para o diretório onde o SBOne indicou estar a DLL AddOnInstallAPI.dll, para não dar "pau"
      // nas funções que utilizam ela
      ExtractTemporaryFile('Vista.cjstyles');
			LoadSkin(ExpandConstant('{tmp}\Vista.cjstyles'), '');
			SetCurrentDir(GetDLLPath(false));
      Result := true;

    //end;     //000

  end;

end;

//*******************************************************************************************************
//  CurStepChanged (função interna do Inno)
//
//*******************************************************************************************************
procedure CurStepChanged(CurStep: TSetupStep);
begin

  // Ao terminar a instalação com sucesso informa ao SB1 o diretório
  // de instalação e dá o sinal de que a instalação terminou
  if CurStep = ssPostInstall then begin
    RegisterAddOnInfo(); // grava a chave de instalação do add-on no Registro
    SetAddOnFolder(ExpandConstant('{app}'));
    EndInstallEx(ExpandConstant('{app}'), true);
    //UnloadDLL('C:\Users\eduardo\Documents\Visual Studio 2010\Projects\MonitoramentoImpostos\MEF\AddOnInstallAPI.dll'); //000
  end;

end;

procedure DeinitializeSetup();
begin
		ShowWindow(StrToInt(ExpandConstant('{wizardhwnd}')), 0);
		UnloadSkin();
    EndInstallEx('', false);
    UnloadDLL('C:\Source\Atlantic\EnvioNfLote\MEF\AddOnInstallAPI.dll');  // instalação cancelada
end;

//*******************************************************************************************************
//  CurUninstallStepChanged (função interna do Inno)
//
//*******************************************************************************************************
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin

  // Ao terminar a desinstalação com sucesso avisa ao SB1
  if CurUninstallStep = usPostUninstall then begin
    UnregisterAddOnInfo(); // Remove do Registro a pasta de instalação do add-on
    EndUninstall(ExpandConstant('{app}'), true);
    //UnloadDLL('C:\Users\eduardo\Documents\Visual Studio 2010\Projects\MonitoramentoImpostos\MEF\AddOnInstallAPI.dll');  //000
  end;

end;

procedure DeinitializeUninstall();
begin
  //EndUnInstall('', false);   //000
  UnloadDLL('C:\Source\Atlantic\EnvioNfLote\MEF\AddOnInstallAPI.dll');
end;
